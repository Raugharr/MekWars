plugins {
    id 'application'
    id 'java-common-conventions'
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
    }
}

dependencies {
    implementation project(':MekWarsCommon')
    implementation project(':MekWarsClient')
}

application {
    mainClassName = 'mekwars.operationseditor.MainOperations'
    applicationDefaultJvmArgs = [
            '-Xmx4096m',
            '-Dsun.awt.disablegrab=true'
    ]
}

ext {
    fileStagingDir = "${buildDir}/files"
    data = "data"
}

jar {
    archiveFileName = 'MekWarsOperationsEditor.jar'
    manifest {
        attributes(
          'Class-Path': configurations.runtimeClasspath.files
              .findAll { it.name.endsWith(".jar") }
              .collect { "${lib}/${it.name}" }.join(' '),
          'Main-Class': mainClassName
        )
    }
}

task stageFiles(type: Copy) {
	file(fileStagingDir).mkdirs()
	File batFile = new File("${fileStagingDir}/${project.tasks.getByName("jar").archiveBaseName.get()}.bat")
	batFile.text = "start /min javaw ${project.application.applicationDefaultJvmArgs.join(' ')} ${jar.archiveFileName.get()}"
	from batFile
	into fileStagingDir
}

distributions {
    main {
        contents {
            from stageFiles
            from jar
            from (project.sourceSets.main.runtimeClasspath.files
                    .findAll { it.name.endsWith(".jar") }) {
                into "${lib}"
            }
            duplicatesStrategy = 'exclude'
        }
    }
}
