plugins {
    id 'application'
    id 'java-common-conventions'
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
    }

    test {
        java {
            srcDirs = ['unittests']
        }
        resources {
            srcDirs = ['unittests/resources']
        }
    }
}

dependencies {
    implementation project(':MekWarsCommon')
	implementation 'org.jsoup:jsoup:1.21.2'
    testImplementation "org.mockito:mockito-junit-jupiter:5.11.0"
    testImplementation "org.junit.jupiter:junit-jupiter:5.10.2"
    testImplementation "org.mockito:mockito-junit-jupiter:5.11.0"
}

application {
    mainClassName = 'mekwars.server.MWServ'
    applicationDefaultJvmArgs = [
            '-Dlog4j2.configurationFile=log4j2.xml',
            '-Xmx4096m',
            '-Dsun.awt.disablegrab=true'
    ]
}

ext {
    fileStagingDir = "${buildDir}/files"
    serverdist = "server-dist"
    data = "${serverdist}/data"
    mmconf = "${serverdist}/mmconf"
    lib = "lib"
}

jar {
    archiveFileName = "MekWarsServer.jar"
    manifest {
        attributes(
          'Class-Path': "MegaMek.jar " + configurations.runtimeClasspath.files
              .findAll { it.name.endsWith(".jar") && !it.name.toLowerCase().startsWith("megamek") }
              .collect { "${lib}/${it.name}" }.join(' '),
          'Main-Class': mainClassName
        )
    }
}

task stageFiles(type: Copy) {
    description = 'Stages files that are to be copied into the distribution.'

	file(fileStagingDir).mkdirs()
	File batFile = new File("${fileStagingDir}/${project.tasks.getByName("jar").archiveBaseName.get()}.bat")
	batFile.text = "start /min javaw ${project.application.applicationDefaultJvmArgs.join(' ')} ${jar.archiveFileName.get()}"
	from batFile
    from serverdist
	into fileStagingDir
}

tasks.named('run') {
    workingDir = file(serverdist)
}

distributions {
    main {
        contents {
            from stageFiles
            from jar
            from "${mmDir}/megamek/build/libs/MegaMek.jar"
            from (project.sourceSets.main.runtimeClasspath.files
                    .findAll { it.name.endsWith(".jar") }) {
                into "${lib}"
            }
            duplicatesStrategy = 'exclude'
        }
    }
}

test {  
    useJUnitPlatform()
}
