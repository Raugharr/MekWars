plugins {
    id 'application'
	id 'java-common-conventions'
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
	}

    test {
        java {
            srcDirs = ['unittests']
        }
    }
}

dependencies {
	implementation project(':MekWarsCommon')
}

application {
	mainClassName = 'mekwars.server.MWServ'
    applicationDefaultJvmArgs = [
			'-Dlog4j2.configurationFile=log4j2.xml',
            '-Xmx4096m',
            '-Dsun.awt.disablegrab=true'
    ]
}

ext {
	fileStagingDir = "${buildDir}/files"
	serverdist = "server-dist"
	data = "${serverdist}/data"
	mmconf = "${serverdist}/mmconf"
}

task stageFiles(type: Copy) {
	description = 'Stages files that are to be copied into the distribution.'

	from serverdist
	into fileStagingDir
}

jar {
	archiveFileName = 'MekWarsServer.jar'
	manifest {
		attributes(
		  'Class-Path': configurations.runtimeClasspath.files.collect { it.getName() }.join(' '),
		  'Main-Class': mainClassName
		)
	}
}

tasks.named('run') {
	workingDir = file(serverdist)
}

distributions {
	main {
		contents {
			from stageFiles
            duplicatesStrategy = 'exclude'
		}
	}
}

test {  
	useJUnitPlatform()
}
