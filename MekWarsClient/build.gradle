plugins {
    id 'application'
    id 'java-common-conventions'
    id 'edu.sc.seis.launch4j' version '2.5.4'
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
        resources {
               srcDirs = ['resources']
        }
    }

    test {
        java {
            srcDirs = ['test']
        }

        resources {
            srcDirs = ['test/resources']
        }
    }
}

dependencies {
    implementation project(':MekWarsCommon')
    implementation 'org.jdatepicker:jdatepicker:1.3.4'

    implementation 'com.formdev:flatlaf:3.4'
    implementation 'com.formdev:flatlaf-extras:3.4.1'

    testImplementation 'org.assertj:assertj-swing:3.17.1'
    testImplementation 'org.assertj:assertj-swing-junit:3.17.1'
}

application {
    mainClassName = 'mekwars.client.MWClient'
}

ext {
    fileStagingDir = "${buildDir}/files"
    clientdist = "client-dist"
    lib = "lib"
    data = "data"
    docs = "docs"
    mmconf = "mmconf"
}

jar {
    archiveFileName = 'MekWars.jar'
    manifest {
        attributes(
          'Class-Path': "MegaMek.jar " + configurations.runtimeClasspath.files
              .findAll { it.name.endsWith(".jar") && !it.name.toLowerCase().startsWith("megamek") }
              .collect { "${lib}/${it.name}" }.join(' '),
            'Main-Class': mainClassName
        )
    }
}

task stageFiles(type: Copy) {
    description = 'Stages files that are to be copied into the distribution.'

    dependsOn gradle.includedBuild('megamek').task(':megamek:stageFiles')

    from clientdist
    into fileStagingDir
}

tasks.named ('run') {
    workingDir = file('client-dist')
}

tasks.named('installDist') {
    doLast {
        def jarTask = tasks.named('jar').get()
        def jvmArgs = (project.application.applicationDefaultJvmArgs ?: []).join(' ')
        def jarName = jarTask.archiveFileName.get()    // MekWars.jar
        def baseName = jarTask.archiveBaseName.get()   // MekWars

        def installDir = file("$buildDir/install/${project.name}")
        File batFile = new File(installDir, "${baseName}.bat")
        batFile.text = "start /min javaw -Dlog4j2.configurationFile=log4j2.xml -Xmx2048m -Dsun.java2d.d3d=false -Dsun.awt.disablegrab=true -Dflatlaf.uiScale=1.0 -jar \"%~dp0MekWars.jar\" %*"
    }
}

distributions {
    main {
        contents {
            from stageFiles
            from jar
            from "${mmDir}/megamek/build/libs/MegaMek.jar"

            from (project.sourceSets.main.runtimeClasspath.files
                    .findAll { it.name.endsWith(".jar") }) {
                into "${lib}"
            }

            // execute build with -PexcludeData to skip data copy
            if (!project.hasProperty('excludeData')) {
                from("${mmDir}/megamek/data/images") {
                    into "data/images"
                }
            }

            duplicatesStrategy = 'exclude'
        }
    }
}

createExe {
    mainClassName = project.mainClassName
    outfile = 'MekWars.exe'
    jar = project.tasks.getByName("jar").archiveFile.get()
            duplicatesStrategy = 'exclude'
}

test {
    jvmArgs "-Dlog4j2.configurationFile=log4j2.xml"
    useJUnitPlatform()
}
