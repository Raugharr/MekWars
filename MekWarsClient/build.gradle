plugins {
    id 'application'
	id 'java-common-conventions'
}

repositories {
    flatDir {
        dirs 'lib' // This tells Gradle to look for dependencies in the 'lib' directory (MekWars/MekWarsClient/lib)
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
		resources {
   			srcDirs = ['resources']
		}
	}
}

dependencies {
	implementation project(':MekWarsCommon')
    implementation 'org.megamek:MegaMek:49.20' // Looks in MekWars/MekWarsClient/lib for the pre-built MegaMek.jar
    // The MegaMek artifact doesn't follow proper naming conventions. To follow proper naming conventions,
    // please rename the MegaMek.jar, built on tag 49.20, to follow convention -> "MegaMek-49.20.jar"
}

application {
	mainClassName = 'mekwars.client.MWClient'
}

ext {
	fileStagingDir = "${buildDir}/files"
	clientdist = "client-dist"
	lib = "lib"
	data = "data"
	docs = "docs"
	mmconf = "mmconf"
}

task stageFiles(type: Copy) {
	description = 'Stages files that are to be copied into the distribution.'

	dependsOn gradle.includedBuild('megamek').task(':megamek:stageFiles')

	from clientdist
	into fileStagingDir
}

jar {
	archiveFileName = 'MekWars.jar'
    manifest {
        attributes(
            'Class-Path': configurations.runtimeClasspath.files.collect { it.getName() }.join(' '),
            'Main-Class': mainClassName
        )
    }
}

tasks.named ('run') {
	workingDir = file('client-dist')
}

distributions {
	main {
		contents {
			from stageFiles
            duplicatesStrategy = 'exclude'
		}
	}
}
